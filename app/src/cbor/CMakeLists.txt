#
# Copyright (c) 2025 Nordic Semiconductor ASA
#
# SPDX-License-Identifier: LicenseRef-Nordic-5-Clause
#

target_sources(app PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/cbor_helper.c)
target_include_directories(app PRIVATE .)

# generate encoder code using zcbor
set(zcbor_command
        zcbor code # Invoke code generation
	--cddl ${CMAKE_CURRENT_SOURCE_DIR}/device_shadow.cddl
	--decode # Generate decoding functions
	--short-names # Attempt to make generated symbol names shorter (at the risk of collision)
	-t desired-object # Create a public API for decoding the "desired-object" type from the cddl file
	--output-cmake device_shadow.cmake # The generated cmake file will be placed here
)
execute_process(COMMAND ${zcbor_command}
		WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
		COMMAND_ERROR_IS_FATAL ANY)

# Include the cmake file generated by zcbor. It adds the
# generated code and the necessary zcbor C code files.
include(${CMAKE_CURRENT_BINARY_DIR}/device_shadow.cmake)

# Ensure that the cmake reconfiguration is triggerred everytime the cddl file changes.
# This ensures that the codec generation is triggered.
set_property(
	DIRECTORY
	PROPERTY
	CMAKE_CONFIGURE_DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/device_shadow.cddl
)

zephyr_link_libraries(device_shadow)
target_link_libraries(device_shadow PRIVATE zephyr_interface)
